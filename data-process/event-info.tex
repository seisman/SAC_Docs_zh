\section{事件信息}
\label{sec:event-info}
相关头段：evla, evlo, evdp, mag, o, nzyear, nzjday, nzhour, nzmin, nzsec, nzmsec

一般来说，从SEED连续波形中解压得到的SAC数据中是没有事件信息的。这就需要用户从
地震目录中获取事件的发震时刻、经度、纬度、深度和震级信息，并将这些信息写入到
SAC文件的头段中。SAC提供了用于可以修改头段变量的命令~\nameref{cmd:chnhdr}，
以及将修改后的头段变量写到磁盘文件的命令~\nameref{cmd:writehdr}
\footnote{
也可以使用``\verb+w over+''将修改写回磁盘文件。二者的区别在于，``\verb+wh+''
仅仅覆盖磁盘文件的头段区，而``\verb+w over+''则覆盖整个文件。这里使用``\verb+wh+''
是因为此处只修改了头段区，而没有对数据做任何修改，使用``\verb+wh+''即可满足要求，
且从执行效率上看``\verb+wh+''比``\verb+w over+''要快不少。}。

\subsection{经纬度、深度与震级}
想要修改事件的经纬度、深度和震级，操作如下：
\begin{SACCode}
SAC> r cdv.?
SAC> ch evla 37.52 evlo -121.68 evdp 5.95   // 修改三个头段变量
SAC> ch mag 5.0                             // 修改一个头段变量
SAC> wh                                     // 将修改后的头段写入文件
\end{SACCode}

\subsection{发震时刻}
通常，需要将发震时刻信息写入SAC头段，并设置SAC文件的参考时刻为发震时刻。
这样设置的好处在于，可以直观地从X轴坐标上读取震相走时。要实现这一操作，需要
用到~\nameref{cmd:chnhdr}~的两个特殊用法。

假设发震时刻为1987年06月22日11时10分10.363秒：
\label{code:origin-time}
\begin{SACCode}
SAC> r ./cdv.?
SAC> ch o gmt 1987 173 11 10 10 363   // 06月22日是第173天
SAC> lh kzdate kztime o

     kzdate = JUN 22 (173), 1987
     kztime = 11:09:56.363
          o = 1.400000e+01       // 发震时刻相对于参考时刻的时间为14秒
SAC> ch allt -14 iztype IO       // 参考时间加14秒，其他时间减14秒
SAC> lh kzdate kztime o

     kzdate = JUN 22 (173), 1987
     kztime = 11:10:10.363
          o = 0.000000e+00
SAC> wh                          // 写回磁盘
\end{SACCode}

在上面的例子中，首先从地震目录中获取了地震的发震时刻，然后计算发震日期对应一年中的第几天，
本例中为第173天，再利用``\verb+ch o gmt yyyy ddd hh mm sss xxx+''
的语法将发震时刻赋值给头段变量~\verb+o+，SAC会自动将发震时刻转换为相对于
参考时刻的相对时间。由于此时SAC文件的参考时刻为``1987-06-22T11:09:56.363''，
而o值对应的时刻为发震时刻``1987-06-22T11:10:10.363''，所以头段变量o的值为发震时刻
相对于参考时刻的时间差，即14s。

将发震时刻写入头段之后，还需要将参考时刻修改为发震时刻，与此同时还要修改所有的相对时间。
``\verb+ch allt xx.xx+''的功能是将所有已定义的相对时间加上~\verb+xx.xx+秒，
同时从参考时刻中减去~\verb+xx.xx+秒，此时参考时刻即为发震时刻，而o值为~\verb+0+。

实际数据处理的时候，不可能像上面的示例那样使用``\verb+lh o+''查看o的值再使用
``\verb+ch allt+''选项，此时就需要使用SAC提供的引用头段变量的值的功能。具体的语法
以及用法会在``\nameref{chap:sac-programming}''一章中介绍。

\begin{SACCode}
SAC> ch o gmt 1987 173 11 10 10 363
SAC> ch allt (0 - &1,o&) iztype IO
\end{SACCode}

需要注意，上例中的``\verb+(0 - &1,o&)+''必须原样输入！由于101.6版本重写了SAC宏
的语法分析器，所以很多用法都发生了改变。你可能会发现，在101.6及其以后的版本中，
\verb+(0-&1,o&)+、\verb+(-&1,o&)+、\verb+(-&1,o)+都
可以正常使用，但是上例中的版本是唯一一个在所有SAC版本中都正确的，为了命令的通用性，
只要记住这个就可以了。

进行实际数据处理时，可以参考第~\ref{call:ch-orign}~节中提供的Perl脚本。
